# Maintainer: Vasily Khoruzhick <anarsoul@gmail.com>

buildarch=8

pkgname=libcedrus-git
pkgver=9b243c4
pkgrel=1
pkgdesc="sunxi Video Engine library"
arch=('aarch64')
url="https://github.com/linux-sunxi/libcedrus"
license=('LGPL')
depends=()
makedepends=('git')
provides=("${pkgname%-git}")
source=("git+https://github.com/linux-sunxi/${pkgname%-git}"
	"90-sunxi-cedrus.rules")
md5sums=('SKIP'
	 '9d4bf897e8d9fb2420b3108606d2dfc8')

pkgver() {
	cd "$srcdir/${pkgname%-git}"
	printf "%s" "$(git describe --tags --long --always | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g')"
}

build() {
	cd "$srcdir/${pkgname%-git}"
	make
}

package() {
	cd "$srcdir/${pkgname%-git}"
	make DESTDIR="$pkgdir/" prefix="/usr" install
	install -d "${pkgdir}"/usr/lib/udev/rules.d
	cp "${srcdir}"/90-sunxi-cedrus.rules "${pkgdir}"/usr/lib/udev/rules.d
}
